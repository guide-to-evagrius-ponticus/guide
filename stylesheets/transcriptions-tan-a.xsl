<?xml version="1.0" encoding="UTF-8"?>
<?xml-model href="schemas/transcriptions.sch" type="application/xml" schematypens="http://purl.oclc.org/dsdl/schematron"?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns="http://www.w3.org/1999/xhtml"
    xmlns:html="http://www.w3.org/1999/xhtml" xmlns:xs="http://www.w3.org/2001/XMLSchema"
    xmlns:tan="tag:textalign.net,2015:ns" xmlns:tei="http://www.tei-c.org/ns/1.0"
    xmlns:array="http://www.w3.org/2005/xpath-functions/array"
    xmlns:map="http://www.w3.org/2005/xpath-functions/map"
    xpath-default-namespace="http://www.w3.org/1999/xhtml" exclude-result-prefixes="#all"
    version="3.0">

    <!-- Configuration of Parabola for reading output -->
    <!-- Catalyzing (main) input: a TAN-A file -->
    <!-- Secondary input: an HTML template -->
    <!-- Primary output: perhaps diagnostics -->
    <!-- Secondary output: one html page per primary alias group -->
    <!-- Relative URLs are resolved under the presumtion that output will be put into a child folder of the root. -->
    
    
    
    <xsl:param name="is-reading-edition" select="true()"/>
    
    <xsl:output method="xhtml"/>
    
    <xsl:import href="../../TAN/TAN-2021/applications/Parabola/Parabola.xsl"/>
    <!--<xsl:import href="../../TAN/TAN-2022/applications/Parabola/Parabola.xsl"/>-->
    <xsl:include href="incl/transcriptions-core.xsl"/>
    
    <xsl:variable name="full-edition-for-reading-target-uri" as="xs:string"
        select="$tan:default-output-directory-resolved || $this-cpg-no || '-full-for-reading.html'"/>
    <xsl:variable name="full-edition-for-search-target-uri" as="xs:string"
        select="$tan:default-output-directory-resolved || $this-cpg-no || '-full-for-search.html'"/>
    
    <xsl:variable name="this-cpg-no" as="xs:string"
        select="analyze-string($tan:doc-uri, '(no)?cpg[\dabcd]+', 'i')/*:match"/>
    <xsl:variable name="edition-title-map" as="map(*)">
        <xsl:map>
            <xsl:map-entry key="'cpg2430'">Evagrius of Pontus, Praktikos</xsl:map-entry>
            <xsl:map-entry key="'cpg2431'">Evagrius of Pontus, Gnostikos</xsl:map-entry>
            <xsl:map-entry key="'cpg2432'">Evagrius of Pontus, Kephalaia gnostika</xsl:map-entry>
            <xsl:map-entry key="'cpg2437'">Evagrius of Pontus, Letters</xsl:map-entry>
            <xsl:map-entry key="'cpg2438'">Evagrius of Pontus, Great Letter (Letter to Melania)</xsl:map-entry>
            <xsl:map-entry key="'cpg2439'">Evagrius of Pontus, Letter on Faith</xsl:map-entry>
            <xsl:map-entry key="'cpg2449'">Evagrius of Pontus, On Masters and Disciples</xsl:map-entry>
            <xsl:map-entry key="'cpg2450'">Evagrius of Pontus, On Evil Thoughts</xsl:map-entry>
            <xsl:map-entry key="'cpg2475'">Evagrius of Pontus, Counsel</xsl:map-entry>
        </xsl:map>
    </xsl:variable>
    
    <!-- Skip all the verbiage in @n vocabulary; skip other nodes -->
    <xsl:template match="tan:tan-vocabulary[tan:IRI[matches(., 'tag:textalign.net,2015:tan-voc:n:.+')]]
        | tei:quote/@defective | tei:cit/@type | tei:alt | tei:w/@xml:id | @instant | @status
        | tei:certainty/@target | tei:certainty/@locus | @break | @anchored | @precision"
        mode="input-pass-1"/>

    <xsl:param name="omit-tei-elements-without-text" as="xs:boolean"
        select="not($is-reading-edition)"/>

    <xsl:variable name="this-cpg-work-vocabulary"
        select="tan:vocabulary('work', (), $tan:self-expanded-vocabulary)/*[tan:IRI[matches(., $this-cpg-no, 'i')]]"/>
    
    <xsl:variable name="intro-opener" as="element()*">
        <xsl:for-each-group select="$this-cpg-work-vocabulary/tan:desc" group-by="normalize-space(.)">
            <div>
                <xsl:if test="position() gt 1">
                    <xsl:attribute name="class" select="'indent'"/>
                </xsl:if>
                <xsl:value-of select="current-grouping-key()"/>
            </div>
        </xsl:for-each-group>
    </xsl:variable>
    <xsl:variable name="about-this-edition-closer" as="element()*">
        <div class="indent">Click Options below to rearrange or (de)select versions, or to learn
            about the underlying sources.</div>
        <div class="indent">The edition below has been generated by TAN Parabola, one of the
            flagship applications of the Text Alignment Network (<a
                href="https://github.com/textalign/TAN-2021">version 2021</a>), based on the <a
                href="https://github.com/Arithmeticus/TAN-Evagrius">master TAN/TEI files</a>. For
            more about the Text Alignment Network, visit <a href="http://textalign.net"
                >textalign.net</a>.</div>
    </xsl:variable>
    
    
    <xsl:param name="preferred-html-title" as="xs:string" select="
            if (map:keys($edition-title-map) = $this-cpg-no) then
                map:get($edition-title-map, $this-cpg-no)
            else
                /*/tan:head/(tan:title, tan:name)[1]/text()"/>
    <xsl:param name="preferred-html-subtitle" as="xs:string" select="
            if ($is-reading-edition) then
                'Comprehensive parallel edition, reading-optimized'
            else
                'Comprehensive parallel edition, search-optimized'"/>
    <xsl:param name="introductory-text" as="item()*">
        <div class="intro" xmlns="http://www.w3.org/1999/xhtml">
            <xsl:copy-of select="$intro-opener"/>
            <div class="about">
                <div class="label">About this edition</div>
                <xsl:choose>
                    <xsl:when test="$is-reading-edition">
                        <div>The full parallel edition below has been optimized for reading. Old
                            Testament references follow the Septuagint. Select TEI markup has been
                            retained. If a passage has a grouped set of versions that are extremely
                            close to each other, they have been combined into one reading, with
                            redlining and embedded statistical charts to show exactly where and how
                            much each version differs from the others. Vertical bars mark the
                            beginning of lines in the source. Annotations marking words have been
                            retained. </div>
                        <div class="indent">Such rich enhancements have a major drawback, in that
                            they break up words and phrases, compromising browser-based searches. If
                            you prioritize finding text, visit the <a
                                href="{$full-edition-for-search-target-uri}">search-optimized
                                version</a>.</div>
                    </xsl:when>
                    <xsl:otherwise>
                        <div>The full parallel edition below has been optimized for search-based
                            research. Old Testament references follow the Septuagint. Anything that
                            might break up words has been dropped: Granular TEI (e.g., milestones)
                            or TAN markup (e.g., tokenization) has been removed; very similar
                            versions are given in full (i.e., no differences or collations are
                            applied). Such alterations support browser-based searches (CTRL+F),
                            which can be quite useful, since this will normally ignore modifying
                            characters, accents, or variations in case. If you prefer some of the
                            features that have been suppressed, visit the <a
                                href="{$full-edition-for-reading-target-uri}">reading-optimized
                                version</a>.</div>

                    </xsl:otherwise>
                </xsl:choose>
                <xsl:copy-of select="$about-this-edition-closer"/>
            </div>
        </div>
    </xsl:param>
    
    
    <xsl:param name="tan:distribute-vocabulary" select="true()"/>
    <xsl:param name="tan:default-output-directory-resolved" as="xs:string"
        select="resolve-uri('../' || $this-cpg-no || '/', static-base-uri())"/>
    <xsl:param name="claim-component-batch-replacements" as="element()*">
        <replace pattern="(refer|allude)s.+to" replacement="cf."/>
    </xsl:param>
    <xsl:param name="add-display-n" as="xs:boolean" select="false()"/>
    <xsl:param name="render-as-diff-threshhold" as="xs:decimal" select="
            if ($is-reading-edition) then
                0.6
            else
                1.0"/>
    <xsl:param name="tan:ignore-case-differences" as="xs:boolean" select="true()"/>
    <xsl:param name="tan:ignore-punctuation-differences" as="xs:boolean" select="true()"/>
    <xsl:param name="tan:ignore-control-format-characters" as="xs:boolean" select="true()"/>
    <xsl:param name="tan:ignore-character-component-differences" as="xs:boolean" select="true()"/>
    
    <xsl:param name="html-template-uri-resolved" select="$gep-template-uri-resolved"/>
    
    <xsl:param name="tan:html-out.remove-what-attributes-regex" as="xs:string" select="'^_'"/>
    
    
    <!-- Negate Parabola's default placement -->
    <xsl:template match="html:body/node()[1]" priority="1" mode="infuse-html-template">
        <xsl:copy>
            <xsl:copy-of select="@*"/>
            <xsl:apply-templates mode="#current"/>
        </xsl:copy>
    </xsl:template>
    <!-- Specify where the content should be placed -->
    <xsl:template match="html:div[@id = 'content']" priority="2" mode="infuse-html-template">
        <xsl:apply-templates select="$input-pass-4" mode="#current"/>
    </xsl:template>
    
    
    <!-- Patch for TAN-2021 -->
    <xsl:template match="*:fix" mode="tan:attributes-not-in-inclusions">
        <xsl:apply-templates select="@* | node()" mode="#current"/>
    </xsl:template>
    
</xsl:stylesheet>
